%%----------------------------------------------------------------------
%% Day 15: Rambunctious Recitation
%%
%%----------------------------------------------------------------------
-module(puzzle15).

-export([start/0]).

start() ->
    Input = aoc:read_file("15.txt", <<",">>, numbers),
    io:format("Puzzle 15, part 1: ~p~n", [part1(Input)]),
    io:format("Puzzle 15, part 2: ~p~n", [part2(Input)]).

%%----------------------------------------------------------------------
%% Part 1
%%----------------------------------------------------------------------
part1(Input) ->
    {N, Map} = read(Input, #{}, 1),
    speak(Map, N, null, 2020 + 1).

%%----------------------------------------------------------------------
%% Part 2
%%----------------------------------------------------------------------
part2(Input) ->
    {N, Map} = read(Input, #{}, 1),
    speak(Map, N, null, 30000000 + 1).

%%----------------------------------------------------------------------
%%
%%----------------------------------------------------------------------
read([], Map, N) -> {N, Map};
read([H | T], Map, N) ->
    read(T, maps:put(H, [N], Map), N + 1).

%%----------------------------------------------------------------------
%%
%%----------------------------------------------------------------------
speak(_, Nth_spoken, Last_spoken, Nth_spoken) ->
    Last_spoken;
speak(Map, Turn, Last_spoken, Nth_spoken) ->
    Spoken = case maps:get(Last_spoken, Map, []) of
                 [X1, X2] -> X1 - X2;
                 _ -> 0
             end,
    Tail = case maps:get(Spoken, Map, []) of
               [] -> [];
               [X | _] -> [X]
           end,
    speak(maps:put(Spoken, [Turn | Tail], Map),
          Turn + 1,
          Spoken,
          Nth_spoken).

%%----------------------------------------------------------------------
%% Unit tests
%%----------------------------------------------------------------------
-ifdef(TEST).

-include_lib("eunit/include/eunit.hrl").

part1_test() ->
    1 = part1([1, 3, 2]),
    10 = part1([2, 1, 3]),
    27 = part1([1, 2, 3]),
    78 = part1([2, 3, 1]),
    438 = part1([3, 2, 1]),
    1836 = part1([3, 1, 2]).

part2_test() -> ok.

% 175594 = part2([0, 3, 6]),
% 2578 = part2([1, 3, 2]),
% 3544142 = part2([2, 1, 3]),
% 261214 = part2([1, 2, 3]),
% 6895259 = part2([2, 3, 1]),
% 18 = part2([3, 2, 1]),
% 362 = part2([3, 1, 2]).
-endif.
